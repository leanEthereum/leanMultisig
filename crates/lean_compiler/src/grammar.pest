WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

// Program structure
program = { SOI ~ import_statement* ~ constant_declaration* ~ function* ~ EOI }

// Imports
import_statement = { "import" ~ filepath ~ ";" }

// Constants
constant_declaration = { "const" ~ identifier ~ "=" ~ (array_literal | expression) ~ ";" }
array_literal = { "[" ~ (array_element ~ ("," ~ array_element)*)? ~ "]" }
array_element = { array_literal | expression }

// Functions
function = { pragma? ~ "fn" ~ identifier ~ "(" ~ parameter_list? ~ ")" ~ inlined_statement? ~ return_count? ~ "{" ~ statement* ~ "}" }
pragma = { "#![assume_always_returns]" }
parameter_list = { parameter ~ ("," ~ parameter)* }
parameter = { const_keyword? ~ mut_keyword? ~ identifier }
const_keyword = { "const" }
inlined_statement = { "inline" }
return_count = { "->" ~ number }

// Statements
statement = {
    forward_declaration |
    if_statement |
    for_statement |
    match_statement |
    return_statement |
    assert_statement |
    debug_assert_statement |
    vec_declaration |
    push_statement |
    assignment
}

// Vector declaration: var = vec![...] (vectors are implicitly mutable for push)
vec_declaration = { identifier ~ "=" ~ vec_literal ~ ";" }

// Push statement: push(vec_var, element) or push(vec_var[i][j], element)
push_statement = { "push" ~ "(" ~ push_target ~ "," ~ vec_element ~ ")" ~ ";" }
push_target = { identifier ~ ("[" ~ expression ~ "]")* }

return_statement = { "return" ~ (tuple_expression)? ~ ";" }


mut_keyword = @{ "mut" ~ !(ASCII_ALPHANUMERIC | "_") }

forward_declaration = { "var" ~ identifier ~ ";" }

// General assignment: LHS is optional, RHS is any expression
// Compound operators (+=, -=, *=, /=) only allow single target without mut (enforced in parser)
assignment = { (assignment_target_list ~ assign_op)? ~ expression ~ ";" }
assign_op = { "+=" | "-=" | "*=" | "/=" | "=" }
assignment_target_list = { assignment_target ~ ("," ~ assignment_target)* }
assignment_target = { (mut_keyword ~ identifier) | array_access_expr | identifier }

if_statement = { "if" ~ condition ~ "{" ~ statement* ~ "}" ~ else_if_clause* ~ else_clause? }

condition = { assumed_bool_expr | comparison }

assumed_bool_expr = { "!!assume_bool" ~ "(" ~ expression ~ ")" }

// Comparisons (shared between conditions and assertions)
comparison = { add_expr ~ comparison_op ~ add_expr }
comparison_op = { "==" | "!=" | "<" }

else_if_clause = { "else" ~ "if" ~ condition ~ "{" ~ statement* ~ "}" }

else_clause = { "else" ~ "{" ~ statement* ~ "}" }

for_statement = { "for" ~ identifier ~ "in" ~ expression ~ ".." ~ expression ~ unroll_clause? ~ "{" ~ statement* ~ "}" }
unroll_clause = { "unroll" }

match_statement = { "match" ~ expression ~ "{" ~ match_arm* ~ "}" }
match_arm = { pattern ~ "=>" ~ "{" ~ statement* ~ "}" }
pattern = { constant_value }

assert_statement = { "assert" ~ comparison ~ ";" }
debug_assert_statement = { "debug_assert" ~ comparison ~ ";" }

// Expressions
tuple_expression = { expression ~ ("," ~ expression)* }
expression = { add_expr }
add_expr = { sub_expr ~ ("+" ~ sub_expr)* }
sub_expr = { mul_expr ~ ("-" ~ mul_expr)* }
mul_expr = { mod_expr ~ ("*" ~ mod_expr)* }
mod_expr = { div_expr ~ ("%" ~ div_expr)* }
div_expr = { exp_expr ~ ("/" ~ exp_expr)* }
exp_expr = { primary ~ ("**" ~ primary)* }
primary = {
    "(" ~ expression ~ ")" |
    log2_ceil_expr |
    next_multiple_of_expr |
    saturating_sub_expr |
    len_expr |
    array_access_expr |
    function_call_expr |
    var_or_constant
}

// Vector literal: vec![elem1, elem2, ...] - compile-time dynamic arrays
vec_literal = { "vec!" ~ "[" ~ (vec_element ~ ("," ~ vec_element)*)? ~ "]" }
vec_element = { vec_literal | expression }
function_call_expr = { identifier ~ "(" ~ tuple_expression? ~ ")" }
log2_ceil_expr = { "log2_ceil" ~ "(" ~ expression ~ ")" }
next_multiple_of_expr = { "next_multiple_of" ~ "(" ~ expression ~ "," ~ expression ~ ")" }
saturating_sub_expr = { "saturating_sub" ~ "(" ~ expression ~ "," ~ expression ~ ")" }
len_expr = { "len" ~ "(" ~ len_argument ~ ")" }
len_argument = { identifier ~ ("[" ~ expression ~ "]")* }
array_access_expr = { identifier ~ ("[" ~ expression ~ "]")+ }

// Basic elements
var_or_constant = { constant_value | identifier }
constant_value = { number | "public_input_start" | "pointer_to_zero_vector" | "pointer_to_one_vector" }

// Lexical elements
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
number = @{ ASCII_DIGIT+ }
filepath = { "\"" ~ filepath_character* ~ "\"" }
filepath_character = { ASCII_ALPHANUMERIC | "-" | "_" | " " | "." | "+" | "/" }
