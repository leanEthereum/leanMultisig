WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

// Program structure
program = { SOI ~ constant_declaration* ~ function+ ~ EOI }

// Constants
constant_declaration = { "const" ~ identifier ~ "=" ~ expression ~ ";" }

// Functions
function = { pragma? ~ "fn" ~ identifier ~ "(" ~ parameter_list? ~ ")" ~ inlined_statement? ~ return_count? ~ "{" ~ statement* ~ "}" }
pragma = { "#![assume_always_returns]" }
parameter_list = { parameter ~ ("," ~ parameter)* }
parameter = { (const_keyword)? ~ identifier }
const_keyword = { "const" }
inlined_statement = { "inline" }
return_count = { "->" ~ number }

// Statements
statement = {
    forward_declaration |
    single_assignment |
    array_assign |
    if_statement |
    for_statement |
    match_statement |
    return_statement |
    break_statement |
    continue_statement |
    function_call |
    assert_eq_statement |
    assert_not_eq_statement  |
    debug_assert_eq_statement |
    debug_assert_not_eq_statement |
    debug_assert_lt_statement
}

return_statement = { "return" ~ (tuple_expression)? ~ ";" }

break_statement = { "break" ~ ";" }
continue_statement = { "continue" ~ ";" }

forward_declaration = { "var" ~ identifier ~ ";" }

single_assignment = { identifier ~ "=" ~ expression ~ ";" }

array_assign = { identifier ~ "[" ~ expression ~ "]" ~ "=" ~ expression ~ ";" }

if_statement = { "if" ~ condition ~ "{" ~ statement* ~ "}" ~ else_if_clause* ~ else_clause? }

condition = { expression | assumed_bool_expr }

assumed_bool_expr = { "!!assume_bool" ~ "(" ~ expression ~ ")" }

else_if_clause = { "else" ~ "if" ~ condition ~ "{" ~ statement* ~ "}" }

else_clause = { "else" ~ "{" ~ statement* ~ "}" }

for_statement = { "for" ~ identifier ~ "in" ~ rev_clause? ~ expression ~ ".." ~ expression ~ unroll_clause? ~ "{" ~ statement* ~ "}" }
rev_clause = { "rev" }
unroll_clause = { "unroll" }

match_statement = { "match" ~ expression ~ "{" ~ match_arm* ~ "}" }
match_arm = { pattern ~ "=>" ~ "{" ~ statement* ~ "}" }
pattern = { constant_value }

function_call = { function_res? ~ identifier ~ "(" ~ tuple_expression? ~ ")" ~ ";" }
function_res = { var_list ~ "=" }
var_list = { identifier ~ ("," ~ identifier)* }

assert_eq_statement = { "assert" ~ add_expr ~ "==" ~ add_expr ~ ";" }
assert_not_eq_statement = { "assert" ~ add_expr ~ "!=" ~ add_expr ~ ";" }

debug_assert_eq_statement = { "debug_assert" ~ add_expr ~ "==" ~ add_expr ~ ";" }
debug_assert_not_eq_statement = { "debug_assert" ~ add_expr ~ "!=" ~ add_expr ~ ";" }
debug_assert_lt_statement = { "debug_assert" ~ add_expr ~ "<" ~ add_expr ~ ";" }

// Expressions
tuple_expression = { expression ~ ("," ~ expression)* }
expression = { neq_expr }
neq_expr = { eq_expr ~ ("!=" ~ eq_expr)* }
eq_expr = { add_expr ~ ("==" ~ add_expr)* }
add_expr = { sub_expr ~ ("+" ~ sub_expr)* }
sub_expr = { mul_expr ~ ("-" ~ mul_expr)* }
mul_expr = { mod_expr ~ ("*" ~ mod_expr)* }
mod_expr = { div_expr ~ ("%" ~ div_expr)* }
div_expr = { exp_expr ~ ("/" ~ exp_expr)* }
exp_expr = { primary ~ ("**" ~ primary)* }
primary = { 
    "(" ~ expression ~ ")" | 
    log2_ceil_expr |
    array_access_expr |
    var_or_constant
}
log2_ceil_expr = { "log2_ceil" ~ "(" ~ expression ~ ")" }
array_access_expr = { identifier ~ "[" ~ expression ~ "]" }

// Basic elements
var_or_constant = { constant_value | identifier }
constant_value = { number | "public_input_start" }

// Lexical elements
identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
number = @{ ASCII_DIGIT+ }
